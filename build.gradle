import java.nio.file.Files

plugins {
    id "fabric-loom" version "1.14.10"
}

loom {
    accessWidenerPath = file("src/main/resources/sleepy.accesswidener")
    runs {
        client {
            runDir "run-dev"
        }
    }
}

base {
    archivesName.set(project.archives_base_name)
    version = project.mod_version
    group = project.maven_group
}

repositories {
    maven {
        name = "Meteor Dev Releases"
        url = "https://maven.meteordev.org/releases"
    }
    maven {
        name = "Meteor Dev Snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }
}

dependencies {
    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Meteor
    def meteorVersion = project.findProperty("meteor_version") ?: "${project.minecraft_version}-SNAPSHOT"
    modImplementation "meteordevelopment:meteor-client:${meteorVersion}"

    // Litematica + malilib (compile only for printer integration)
    modCompileOnly files("libs/litematica-fabric-1.21.4-0.21.6.jar")
    modCompileOnly files("libs/malilib-fabric-1.21.4-0.23.5.jar")
}

tasks.register("syncModsDir") {
    def modsDir = file("run-dev/mods")
    def targetDir = file("run-dev/mods-${project.minecraft_version}")
    inputs.property("minecraft_version", project.minecraft_version)
    outputs.dir(modsDir)
    doLast {
        if (!targetDir.exists()) {
            throw new GradleException("Missing mods folder: ${targetDir}. Create it first.")
        }

        def modsPath = modsDir.toPath()
        def targetPath = targetDir.toPath()
        def parent = modsPath.getParent()
        if (parent == null) {
            throw new GradleException("Cannot determine parent directory for ${modsDir}")
        }

        if (Files.exists(modsPath)) {
            if (Files.isSymbolicLink(modsPath)) {
                def existingTarget = Files.readSymbolicLink(modsPath)
                def resolved = parent.resolve(existingTarget).normalize()
                if (resolved == targetPath.normalize()) return
                Files.delete(modsPath)
            } else {
                def currentFiles = modsDir.listFiles()?.collect { it.name }?.sort() ?: []
                def targetFiles = targetDir.listFiles()?.collect { it.name }?.sort() ?: []
                if (currentFiles != targetFiles) {
                    throw new GradleException("run-dev/mods does not match ${targetDir.name}. Move or clean it before running.")
                }
                modsDir.deleteDir()
            }
        }

        def relativeTarget = parent.relativize(targetPath)
        Files.createSymbolicLink(modsPath, relativeTarget)
    }
}

tasks.named("processResources") {
    inputs.property("version", project.version)
    inputs.property("mc_version", project.minecraft_version)

    def propertyMap = [
        "version"   : project.version,
        "mc_version": project.minecraft_version,
    ]

    filesMatching("fabric.mod.json") {
        expand(propertyMap)
    }
}

tasks.named("jar") {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

tasks.matching { it.name == "runClient" }.configureEach {
    dependsOn("syncModsDir")
}
